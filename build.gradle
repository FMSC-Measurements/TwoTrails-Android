apply plugin: 'com.android.application'

android {
    compileSdkVersion 32
    buildToolsVersion '31.0.0'

    def versionPropsFile = file('version.properties')

    def vName = ""
    def vCode = 0

    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        vCode = versionProps['VERSION_CODE'].toInteger()

        def major = versionProps['MAJOR'].toInteger()
        def minor = versionProps['MINOR'].toInteger()
        def revision = versionProps['REVISION'].toInteger()
        def build = versionProps['BUILD'].toInteger() + 1

        versionProps['BUILD'] = build.toString()

        versionProps.store(versionPropsFile.newWriter(), null)

        vName = String.format("%d.%d.%d-%d", major, minor, revision,build)
    }


    defaultConfig {
        applicationId "com.usda.fmsc.twotrails"
        minSdkVersion 28
        targetSdkVersion 32
        versionCode vCode
        versionName vName
        multiDexEnabled true

        resValue "string", "arcgis_runtime_license", property("TwoTrails.ArcGIS.RuntimeLicenseKey") //100.7.0
        resValue "string", "dev_email_addr", property("TwoTrails.Dev.EmailAddress")
    }

    signingConfigs {
        release_config {
            keyAlias 'ReleaseKey'
            keyPassword project.property("TwoTrails.release_pass")
            storeFile file(project.property("TwoTrails.release_keystore"))
            storePassword project.property("TwoTrails.release_pass")
        }

        debug_config {
            keyAlias 'DebugKey'
            keyPassword project.property("TwoTrails.debug_pass")
            storeFile file(project.property("TwoTrails.debug_keystore"))
            storePassword project.property("TwoTrails.debug_pass")
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'
            signingConfig signingConfigs.release_config
            resValue "string", "maps_api_key", project.property("TwoTrails.release_maps_key")

            ndk {
                abiFilters"arm64-v8a"
//                abiFilters "armeabi-v7a", "arm64-v8a"
            }
        }

        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug_config
            resValue "string", "maps_api_key", project.property("TwoTrails.debug_maps_key")

            ndk {
                abiFilters "arm64-v8a"
//                abiFilters  "armeabi-v7a", "arm64-v8a", "x86"
            }
        }
    }

    flavorDimensions "version"

    productFlavors {
        latest {
            dimension "version"
        }

        legacy {
            dimension "version"
            minSdkVersion 28
            applicationIdSuffix ".legacy"
            versionNameSuffix "-legacy"
            ndk {
                abiFilters  "arm64-v8a", "armeabi-v7a"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES.txt', 'META-INF/LICENSE.txt', 'META-INF/NOTICE.txt', 'META-INF/NOTICE', 'META-INF/LICENSE', 'META-INF/DEPENDENCIES', 'META-INF/notice.txt', 'META-INF/license.txt', 'META-INF/dependencies.txt', 'META-INF/LGPL2.1']
        }
    }

    useLibrary 'org.apache.http.legacy'
    namespace 'com.usda.fmsc.twotrails'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':AndroidEx')
    implementation project(':GeoSpatial')
    implementation project(':Utilities')
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.mediarouter:mediarouter:1.3.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.3'
    implementation 'com.google.android.gms:play-services-maps:18.1.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.google.android.material:material:1.6.1'
    implementation ('com.github.ozodrukh:CircularReveal:2.1.0') {
        transitive = true
    }
    implementation 'com.sothree.slidinguppanel:library:3.3.0'
    implementation 'com.larswerkman:HoloColorPicker:1.5'
    implementation 'com.esri.arcgisruntime:arcgis-android:100.10.0'
    implementation 'com.esri.arcgisruntime:arcgis-android-toolkit:100.10.0'
    implementation 'com.android.volley:volley:1.2.1'
    implementation 'joda-time:joda-time:2.10.10'
    implementation ('jp.wasabeef:recyclerview-animators:4.0.2') {
        transitive = true
    }
//    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    implementation 'me.zhanghai.android.materialprogressbar:library:1.1.4'
    implementation 'org.apache.commons:commons-csv:1.2'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.navigation:navigation-fragment:2.5.0'
    implementation 'androidx.navigation:navigation-ui:2.5.0'
    implementation 'com.github.mukeshsolanki:MarkdownView-Android:1.1.1'
}