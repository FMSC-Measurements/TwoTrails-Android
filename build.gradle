apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    def versionPropsFile = file('version.properties')

    def vName = ""
    def vCode = 0

    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        vCode = versionProps['VERSION_CODE'].toInteger()

        def major = versionProps['MAJOR'].toInteger()
        def minor = versionProps['MINOR'].toInteger()
        def revision = versionProps['REVISION'].toInteger()
        def build = versionProps['BUILD'].toInteger() + 1

        versionProps['BUILD'] = build.toString()

        versionProps.store(versionPropsFile.newWriter(), null)

        vName = String.format("%d.%d.%d-%d", major, minor, revision,build)
    }// else {
    //    throw new GradleException("Could not read version.properties")
    //}


    defaultConfig {
        applicationId "com.usda.fmsc.twotrails"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode vCode
        versionName vName
        multiDexEnabled true


        resValue "string", "arcgis_client_id", property("TwoTrails.ArcGIS.ClientID") //100.2.9
        resValue "string", "arcgis_client_secret", property("TwoTrails.ArcGIS.ClientSecret") //100.2.9
        resValue "string", "arcgis_runtime_license", property("TwoTrails.ArcGIS.RuntimeLicenseKey") //100.6.0
        resValue "string", "dev_email_addr", property("TwoTrails.Dev.EmailAddress")

        //temp for getting to work on devices with old ArcGIS api
        //TODO remove for release on 64bit
//        ndk {
//            abiFilters "armeabi", "armeabi-v7a", "x86", "mips"
//        }
    }

    signingConfigs {
        release_config {
            keyAlias 'ReleaseKey'
            keyPassword project.property("TwoTrails.release_pass")
            storeFile file(project.property("TwoTrails.release_keystore"))
            storePassword project.property("TwoTrails.release_pass")
        }

        debug_config {
            keyAlias 'DebugKey'
            keyPassword project.property("TwoTrails.debug_pass")
            storeFile file(project.property("TwoTrails.debug_keystore"))
            storePassword project.property("TwoTrails.debug_pass")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
            signingConfig signingConfigs.release_config
            resValue "string", "maps_api_key", project.property("TwoTrails.release_maps_key")
        }

        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug_config
            resValue "string", "maps_api_key", project.property("TwoTrails.debug_maps_key")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    productFlavors {

    }

    dexOptions {
//        incremental true  //last resort
        javaMaxHeapSize "4g" //1g is too small
//        jumboMode false
        preDexLibraries true //faster incremental builds
//        threadCount 8
    }

    //so arcgis can build successfully
    packagingOptions {
//        exclude 'META-INF/LICENSE'
//        exclude 'META-INF/LGPL2.1'
//        exclude 'META-INF/NOTICE'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }

    useLibrary 'org.apache.http.legacy'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':AndroidEx')
    implementation project(':GeoSpatial')
    implementation project(':Utilities')
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.mediarouter:mediarouter:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.exifinterface:exifinterface:1.0.0'
    implementation 'joda-time:joda-time:2.9.2'
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    implementation 'me.zhanghai.android.materialprogressbar:library:1.1.4'
    implementation 'com.sothree.slidinguppanel:library:3.3.0'
    implementation 'jp.wasabeef:recyclerview-animators:3.0.0'
    implementation 'com.larswerkman:HoloColorPicker:1.5'
    implementation 'org.apache.commons:commons-csv:1.2'
    implementation 'com.mcxiaoke.volley:library:1.0.19'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.esri.arcgisruntime:arcgis-android:100.6.0'
    implementation 'androidx.preference:preference:1.1.0'
    //implementation 'com.applandeo:material-file-picker:1.0.0'
}