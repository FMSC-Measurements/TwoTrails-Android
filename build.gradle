apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    def versionPropsFile = file('version.properties')

    def vName = "0.9"
    def vCode = 0

    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        vCode = versionProps['VERSION_CODE'].toInteger() + 1

        def major = versionProps['MAJOR'].toInteger()
        def minor = versionProps['MINOR'].toInteger()
        def build = versionProps['BUILD'].toInteger() + 1

        versionProps['BUILD'] = build.toString()

        versionProps.store(versionPropsFile.newWriter(), null)

        vName = String.format("%d.%d.%d", major, minor, build)
    } else {
        throw new GradleException("Could not read version.properties")
    }


    defaultConfig {
        applicationId "com.usda.fmsc.twotrails"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode vCode
        versionName vName
        multiDexEnabled true

        resValue "string", "arcgis_client_id", project.property("TwoTrails.ArcGIS.ClientID")
        resValue "string", "arcgis_client_secret", project.property("TwoTrails.ArcGIS.ClientSecret")
    }

    signingConfigs {
        release_config {
            keyAlias 'ReleaseKey'
            keyPassword project.property("TwoTrails.release_pass")
            storeFile file(project.property("TwoTrails.release_keystore"))
            storePassword project.property("TwoTrails.release_pass")
        }

        debug_config {
            keyAlias 'DebugKey'
            keyPassword project.property("TwoTrails.debug_pass")
            storeFile file(project.property("TwoTrails.debug_keystore"))
            storePassword project.property("TwoTrails.debug_pass")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
            signingConfig signingConfigs.release_config
            resValue "string", "maps_api_key", project.property("TwoTrails.release_maps_key")
        }

        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug_config
            resValue "string", "maps_api_key", project.property("TwoTrails.debug_maps_key")
        }
    }

    productFlavors {

    }

    dexOptions {
//        incremental true  //last resort
        javaMaxHeapSize "4g" //1g is too small
//        jumboMode false
        preDexLibraries true //faster incremental builds
//        threadCount 8
    }

    //so arcgis can build successfully
    packagingOptions {
//        exclude 'META-INF/LICENSE'
//        exclude 'META-INF/LGPL2.1'
//        exclude 'META-INF/NOTICE'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }

    useLibrary 'org.apache.http.legacy'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':AndroidEx')
    compile project(':GeoSpatial')
    compile project(':Utilities')
    compile 'com.google.android.gms:play-services:10.2.0'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.android.support:recyclerview-v7:25.2.0'
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:cardview-v7:25.2.0'
    compile 'com.android.support:support-v4:25.2.0'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:design:25.2.0'
    compile 'joda-time:joda-time:2.9.2'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    compile 'me.zhanghai.android.materialprogressbar:library:1.1.4'
    compile 'com.esri.arcgis.android:arcgis-android:10.2.7'
    compile 'com.sothree.slidinguppanel:library:3.3.0'
    compile 'jp.wasabeef:recyclerview-animators:2.2.0'
    compile 'com.nononsenseapps:filepicker:2.5.2'
    compile 'com.larswerkman:HoloColorPicker:1.5'
    compile 'org.apache.commons:commons-csv:1.2'
    compile 'com.mcxiaoke.volley:library:1.0.19'
}
